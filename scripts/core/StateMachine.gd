extends Node
class_name StateMachine

signal state_changed(current_state: Node)

@export var start_state: NodePath
var states_map := {}

var states_stack := []
var current_state: Node = null
var _active: bool = false:
	set(value):
		_active = value
		set_active(value)


func _enter_tree() -> void:
	var initial_state: Node
	if start_state.is_empty():
		initial_state = get_child(0)
	else:
		initial_state = get_node(start_state)
	for child in get_children():
		child.finished.connect(_change_state)
	initialize(initial_state)


func initialize(initial_state: Node) -> void:
	_active = true
	states_stack.push_front(initial_state)
	current_state = states_stack[0]
	current_state.enter()


func set_active(value: bool) -> void:
	set_physics_process(value)
	set_process_input(value)
	if not _active:
		states_stack = []
		current_state = null


func _unhandled_input(input_event: InputEvent) -> void:
	current_state.handle_input(input_event)


func _physics_process(delta: float) -> void:
	current_state.update(delta)


func _on_animation_finished(anim_name: String) -> void:
	if not _active:
		return
	current_state._on_animation_finished(anim_name)


func _change_state(state_name: String) -> void:
	if not _active:
		return
	current_state.exit()

	if state_name == "previous":
		states_stack.pop_front()
	else:
		states_stack[0] = states_map[state_name]

	current_state = states_stack[0]
	state_changed.emit(current_state)

	if state_name != "previous":
		current_state.enter()
